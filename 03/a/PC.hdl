// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Mux16(a=prev,b=increment,sel=inc,out=v);
    Mux16(a=v,b=in,sel=load,out=w);
    Mux16(a=w,b=false,sel=reset,out=in1);
    Or8Way(in[0]=reset,in[1]=load,in[2]=inc,out=load1);
    Register(in=in1,load=load1,out=out,out=prev);
    Inc16(in=prev,out=increment);
}
